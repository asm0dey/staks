buildscript {
    ext {
        atrium_version = '0.12.0'
        kotest_version = "4.2.0.RC2"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'info.solidsoft.pitest' version '1.5.2'
    id 'maven'
    id "io.gitlab.arturbosch.detekt" version "1.14.1"
    id "org.jetbrains.dokka" version "1.4.20"
    // publishing
    id 'maven-publish'
    id "io.codearte.nexus-staging" version "0.30.0"
    id 'signing'
    id "de.marcphilipp.nexus-publish" version "0.3.0"
}

group 'com.github.asm0dey'
version '0.0.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.fasterxml.woodstox:woodstox-core:6.2.3'
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "ch.tutteli.atrium:atrium-infix-en_GB:$atrium_version"
    testImplementation "io.kotest:kotest-plugins-pitest:$kotest_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

pitest {
    testPlugin = "Kotest"
    targetClasses = ['staks.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
    avoidCallsTo = ["kotlin.jvm.internal", "kotlinx.coroutines"]
}

detekt {
    parallel = false
    config = files "code_quality_tools/detekt.yml"
    reports {
        xml { enabled = true }
        html { enabled = true }
    }
}

tasks.detekt.jvmTarget = "1.8"

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/dokka/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(sourcesJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            pom {
                groupId = 'com.github.asm0dey'
                artifactId = 'staks'

                from components.kotlin

                name = 'Staks'
                description = 'Kotlin wrapper for StAX which allows object-oriented building of objects'
                url = 'https://github.com/asm0dey/staks'
                licenses {
                    license {
                        name = 'BSD-3-Clause License'
                        url = 'https://raw.githubusercontent.com/asm0dey/staks/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'asm0dey'
                        name = 'Pasha Finkelshteyn'
                        email = 'pavel.finkelshtein@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/asm0dey/staks.git'
                    developerConnection = 'scm:git:ssh://github.com/asm0dey/staks.git'
                    url = 'https://github.com/asm0dey/staks/'
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotsRepoUrl  = "https://oss.sonatype.org/content/repositories/snapshots"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "$ossrhAsm0dey"
                password "$ossrhAsm0deyPass"
            }
        }
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

tasks.withType(Sign) {
    onlyIf { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    useGpgCmd()
    sign publishing.publications.mavenJava
}
